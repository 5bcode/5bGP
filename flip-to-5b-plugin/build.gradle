plugins {
	id 'java'
}

repositories {
	mavenLocal()
	maven {
		url = 'https://repo.runelite.net'
		content {
			includeGroupByRegex("net\\.runelite.*")
		}
	}
	mavenCentral()
}

def runeLiteVersion = 'latest.release'

dependencies {
	compileOnly group: 'net.runelite', name:'client', version: runeLiteVersion

	compileOnly 'org.projectlombok:lombok:1.18.30'
	annotationProcessor 'org.projectlombok:lombok:1.18.30'

	testImplementation 'junit:junit:4.12'
	testImplementation group: 'net.runelite', name:'client', version: runeLiteVersion
	testImplementation group: 'net.runelite', name:'jshell', version: runeLiteVersion
}

group = 'com.flipto5b'
version = '1.0-SNAPSHOT'

tasks.withType(JavaCompile).configureEach {
	options.encoding = 'UTF-8'
	options.release.set(11)
}

tasks.register('shadowJar', Jar) {
	dependsOn configurations.testRuntimeClasspath
	manifest {
		attributes('Main-Class': 'com.flipto5b.FlipTo5BPluginTest', 'Multi-Release': true)
	}

	duplicatesStrategy = DuplicatesStrategy.EXCLUDE
	from sourceSets.main.output
	from sourceSets.test.output
	from {
		configurations.testRuntimeClasspath.collect { file ->
			file.isDirectory() ? file : zipTree(file)
		}
	}

	exclude 'META-INF/INDEX.LIST'
	exclude 'META-INF/*.SF'
	exclude 'META-INF/*.DSA'
	exclude 'META-INF/*.RSA'
	exclude '**/module-info.class'

	group = BasePlugin.BUILD_GROUP
	archiveClassifier.set('shadow')
	archiveFileName.set("${rootProject.name}-${project.version}-all.jar")
}

task runClient(type: JavaExec) {
	group = 'execution'
	description = 'Runs the RuneLite client with the plugin loaded.'
	classpath = sourceSets.test.runtimeClasspath
	mainClass = 'com.flipto5b.FlipTo5BPluginTest'
	jvmArgs += [
		'-ea',
		'--add-opens', 'java.base/java.lang=ALL-UNNAMED',
		'--add-opens', 'java.base/java.net=ALL-UNNAMED',
		'--add-opens', 'java.base/java.nio=ALL-UNNAMED',
		'--add-opens', 'java.base/java.nio.file=ALL-UNNAMED',
		'--add-opens', 'java.base/java.lang.reflect=ALL-UNNAMED',
		'--add-opens', 'java.base/java.text=ALL-UNNAMED',
		'--add-opens', 'java.base/java.util=ALL-UNNAMED',
		'--add-opens', 'java.base/java.util.concurrent=ALL-UNNAMED',
		'--add-opens', 'java.base/java.util.concurrent.atomic=ALL-UNNAMED',
		'--add-opens', 'java.desktop/java.awt=ALL-UNNAMED',
		'--add-opens', 'java.desktop/java.awt.dnd=ALL-UNNAMED',
		'--add-opens', 'java.desktop/java.awt.event=ALL-UNNAMED',
		'--add-opens', 'java.desktop/java.awt.image=ALL-UNNAMED',
		'--add-opens', 'java.desktop/javax.swing=ALL-UNNAMED',
		'--add-opens', 'java.desktop/javax.swing.plaf.basic=ALL-UNNAMED',
		'--add-opens', 'java.desktop/sun.awt=ALL-UNNAMED',
		'--add-opens', 'java.desktop/sun.awt.image=ALL-UNNAMED'
	]
}